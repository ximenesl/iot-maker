CÓDIGO DO PROJETO SENSOR TEMPERATURA E UMIDADE: 

#include <WiFi.h>
#include <ThingSpeak.h>
#include <Adafruit_Sensor.h>
#include <DHT.h>

// Configurações do Wi-Fi
const char* ssid = "Suarede";
const char* password = "suasenha";

// Configurações do ThingSpeak
unsigned long myChannelNumber = 3016537; // Substitua pelo seu Channel Number
const char * myWriteAPIKey = "GN49RMU53ZTJT60N"; // Substitua pela sua Write API Key

// Pinos do sensor DHT22
#define DHTPIN 1    // Conectado ao pino GPIO1 do ESP32-C3
#define DHTTYPE DHT11   // O sensor utilizado é o DHT22

// Inicializa o sensor DHT
DHT dht(DHTPIN, DHTTYPE);

// Objeto ThingSpeak
WiFiClient  client;

// Intervalo entre as atualizações (em milissegundos)
const long updateInterval = 20000; // 20 segundos
unsigned long lastUpdate = 0;

void setup() {
  Serial.begin(115200);
  delay(10);

  // Inicializa o sensor DHT
  dht.begin();
  
  // Conexão Wi-Fi
  Serial.println();
  Serial.print("Conectando a ");
  Serial.println(ssid);

  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi conectado!");
  Serial.println("Endereço de IP: ");
  Serial.println(WiFi.localIP());

  // Inicializa o cliente ThingSpeak
  ThingSpeak.begin(client);
}

void loop() {
  // Verifica se o tempo de atualização já passou
  if (millis() - lastUpdate > updateInterval) {
    lastUpdate = millis();

    // Lê os dados do sensor
    float umidade = dht.readHumidity();
    float temperatura = dht.readTemperature();

    // Checa se a leitura falhou
    if (isnan(umidade) || isnan(temperatura)) {
      Serial.println("Falha ao ler o sensor DHT!");
      return;
    }

    // Exibe os dados no monitor serial
    Serial.print("Umidade: ");
    Serial.print(umidade);
    Serial.print(" %");
    Serial.print("\tTemperatura: ");
    Serial.print(temperatura);
    Serial.println(" *C");

    // Envia os dados para o ThingSpeak
    ThingSpeak.setField(1, temperatura);
    ThingSpeak.setField(2, umidade);
    
    int x = ThingSpeak.writeFields(myChannelNumber, myWriteAPIKey);
    if(x == 200){
      Serial.println("Atualização do canal ThingSpeak bem sucedida.");
    } else {
      Serial.println("Problema ao escrever no canal. HTTP erro: " + String(x));
    }
  }
}